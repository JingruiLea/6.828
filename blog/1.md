# 用汇编编写x86“ Hello world”引导程序

## TL; DR

引导后，计算机的BIOS从引导设备中读取512个字节，如果在这512个字节的末尾检测到两个字节的“幻数”，则将这512个字节中的数据作为代码加载并运行。

这种代码称为“引导加载程序”（或“引导扇区”），我们正在编写少量汇编代码，以使虚拟机运行我们的代码并显示“ Hello world”。引导加载程序也是引导操作系统的第一阶段。

## x86计算机启动时会发生什么

您可能想知道当您按下计算机上的“电源”按钮时会发生什么。好了，无需太详细介绍-在准备好硬件并启动了初始BIOS代码以读取设置并检查系统之后，BIOS开始查看已配置的潜在启动设备以执行某些操作。

它通过从启动设备中读取前512个字节并检查这512个字节中的后两个是否包含幻数（`0x55AA`）来完成此操作。如果这就是最后两个字节，那么BIOS将512字节移至内存地址`0x7c00`，并将512字节开头的内容视为代码，即所谓的**bootloader**。在本文中，我们将编写这样的代码，并将其打印为“ Hello World！”文本。然后进入无限循环
实际的引导加载程序通常将实际的操作系统代码加载到内存中，将CPU更改为所谓的[*保护模式，*](https://en.wikipedia.org/wiki/Protected_mode)然后运行实际的操作系统代码。

## 使用GNU汇编程序的x86汇编入门

为了使我们的生活更轻松（更简单！）并使之更加有趣，我们将在引导程序中使用x86汇编语言。本文将使用GNU汇编器从我们的代码中创建二进制可执行文件，并且GNU汇编器使用“ AT＆T语法”而不是广泛使用的“英特尔语法”。我将在本文结尾处以Intel语法重复该示例。

对于**不**熟悉x86汇编语言和/或GNU汇编器的那些人，我创建了[此描述](https://gist.github.com/AVGP/85037b51856dc7ebc0127a63d6a601fa)，它说明了足够的汇编知识，可以帮助您快速上手本文的其余部分。本文中的汇编代码也将被注释，因此您应该能够在不了解汇编详细信息的情况下浏览代码片段。

## 准备好我们的代码

好的，到目前为止，我们知道：我们需要创建一个512字节的二进制文件，`0x55AA`该文件的末尾包含该文件。还值得一提的是，无论您使用的是32位还是64位x86处理器，在启动时该处理器都将以**16位实模式运行**，因此我们的程序需要对此进行处理。

让我们`boot.s`为汇编源代码创建文件，并告诉GNU汇编器我们将使用16位：

```asm
.code16 # tell the assembler that we're using 16 bit mode
```

啊，这太棒了！接下来，我们应该为我们的程序提供一个起点，并将其提供给链接器（稍后再介绍）：

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  jmp init # jump to "init"
```

**注意**您可以随意命名标签。标准是，`_start`但是我选择`init`说明您可以称之为任何东西。

很好，现在我们甚至遇到了无限循环，因为我们一直跳到标签，然后再次跳到标签...

是时候通过运行GNU汇编器（`as`）将我们的代码转换成一些二进制代码，看看我们得到了什么：

```shell
as -o boot.o boot.s
ls -lh .
784 boot.o
152 boot.s
```

*哇，等等！*我们的输出已经是784个字节了？但是我们的引导程序只有512个字节！

好吧，大多数时候，开发人员可能会对为他们所针对的操作系统创建可执行文件感兴趣，即`exe`（Windows），`elf`（Unix）文件。这些文件具有标头（读取：附加的前导字节），并且通常加载一些系统库以访问操作系统功能。

我们的情况是不同的：我们什么都不想要，只希望二进制代码供BIOS在启动时执行。

通常，汇编器会生成一个可以运行的ELF或EXE文件，但是我们需要执行一个附加步骤，以剥离这些文件中不需要的附加数据。我们可以在`ld`此步骤中使用链接器（称为GNU的链接器）。

链接器通常用于将各种库和来自其他工具（例如编译器或汇编器）的二进制可执行文件组合到一个最终文件中。在我们的例子中，我们想产生一个“普通二进制文件”，因此我们将在运行它时传递`--oformat binary`给`ld`它。我们还想指定程序的开始位置，因此我们通过使用标志告诉链接器将`init`代码中的开始标签（我称为）用作程序的*入口点*`-e init`。

运行该命令时，会得到更好的结果：

```shell
as -o boot.o boot.s
ld -o boot.bin --oformat binary -e init boot.o
ls -lh .
  3 boot.bin
784 boot.o
152 boot.s
```

（[xnumbersx](https://www.reddit.com/user/xnumbersx)发现[错别字](https://www.reddit.com/user/xnumbersx)）

好的，三个字节听起来要好得多，但这不会启动，因为它缺少`0x55AA`二进制数的字节511和512处的幻数...

## 使它可启动

幸运的是，我们可以在二进制文件中填充一堆零，并在最后添加幻数作为数据。
让我们开始添加零，直到我们的二进制文件长510个字节（因为最后两个字节将是幻数）。

我们可以使用预处理器指令`.fill`from `as`来做到这一点。语法是`.fill, count,size,value`-它将`count`乘以`size`值的字节数乘以字节，`value`无论我们在何处将此指令写入汇编代码中`boot.s`。

但是我们如何知道需要填充多少个字节？方便地，汇编程序再次帮助了我们。我们总共需要510个字节，所以我们将510-（代码的字节大小）字节填充为零。但是“我们的代码的字节大小”是多少？幸运的是，`as`有一个帮助程序可以告诉我们所生成的二进制文件中当前字节的位置：`.`-我们也可以获取标签的位置。因此，我们的代码大小将等于`.`代码之后当前位置减去代码中第一个语句的位置（即位置`init`）的大小。因此`.-init`，在最终的二进制文件中返回代码生成的字节数。

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  jmp init # jump to "init"

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
as -o boot.o boot.s
ld -o boot.bin --oformat binary -e init boot.s
ls -lh .
 510 boot.bin
1.3k boot.o
 176 boot.s
```

我们到达那里-仍然缺少我们魔术字的最后两个字节：

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  jmp init # jump to "init"

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
.word 0xaa55 # magic bytes that tell BIOS that this is bootable
```

哦，等等...如果魔术字节是`0x55aa`，我们为什么在这里交换它们？
那是因为x86是[little endian](https://en.wikipedia.org/wiki/Endianness)，所以字节在内存中被交换。

现在，如果我们生成一个更新的二进制文件，则它的长度为512个字节。

## 引导我们的引导程序

从理论上讲，您可以将此二进制文件写入USB驱动器，软盘或计算机希望从中启动的任何其他文件的前512个字节中，但是让我们使用一个简单的x86模拟器（就像虚拟机）来代替。

我将为此使用具有x86系统架构的QEmu：

```shell
qemu-system-x86_64 boot.bin
```

运行此命令会产生相对不明显的内容：
![img](http://50linesofco.de/images/post-images/qemu-first-boot.png)

QEmu停止寻找可启动设备的事实意味着我们的引导加载程序可以正常工作-但它什么也没做！

为了证明这一点，我们可以通过将汇编代码更改为以下内容来导致重新启动循环而不是无能为力的无限循环：

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  ljmpw $0xFFFF, $0 # jumps to the "reset vector", doing a reboot

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
.word 0xaa55 # magic bytes that tell BIOS that this is bootable
```

这个新命令`ljmpw $0xFFFF, $0`跳转到所谓的[**复位向量**](https://en.wikipedia.org/wiki/Reset_vector)。
这实际上意味着在系统重新引导后实际上不重新引导就重新执行第一条指令。有时称为“热重启”。

## 使用BIOS打印文本

好的，让我们从打印单个字符开始。
我们没有可用的操作系统或库，因此我们不能只是打电话给它`printf`或它的一个朋友而已。

幸运的是，我们的BIOS仍然可用并且可以访问，因此我们可以利用其功能。这些功能（以及不同硬件提供的一系列功能）可以通过所谓的[中断来使用](https://en.wikipedia.org/wiki/Interrupt)。

在[Ralf Brown的中断列表中，](http://www.ctyme.com/intr/alpha.htm)我们可以找到视频中断0x10。

单个中断可以执行许多不同的功能，通常可以通过将AX寄存器设置为特定值来选择这些功能。在我们的例子中，[“ Teletype”功能](http://www.ctyme.com/intr/rb-0106.htm)听起来很不错，它将打印输入的字符`al`并自动使光标前进。好漂亮！我们可以通过设置`ah`为来选择该函数`0xe`，将要打印的ASCII代码放入其中`al`，然后调用`int 0x10`：

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  mov $0x0e41, %ax # sets AH to 0xe (function teletype) and al to 0x41 (ASCII "A")
  int $0x10 # call the function in ah from interrupt 0x10
  hlt # stops executing

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
.word 0xaa55 # magic bytes that tell BIOS that this is bootable
```

现在，我们将必要的值加载到`ax`寄存器中，调用中断0x10并暂停执行（使用`hlt`）。

当我们运行`as`并`ld`获取更新的引导程序时，QEmu向我们显示了以下内容：

![img](http://50linesofco.de/images/post-images/qemu-single-character.png)

我们甚至可以看到光标在下一个位置闪烁，因此该功能应该易于用于较长的消息，对吗？

## 我们最后的hello-world-bootloader

为了显示完整的消息，我们需要一种将这些信息存储在二进制文件中的方法。我们可以像在二进制文件末尾存储魔术字那样执行此操作，但是将使用与`.byte`存储完整字符串不同的指令。`as`幸运的是自带`.ascii`和`.asciz`字符串。它们之间的区别是`.asciz`自动添加另一个设置为零的字节。稍后会派上用场，因此我们选择`.asciz`了数据。
另外，我们将使用标签使我们能够访问该地址：

```asm
.code16
.global init # makes our label "init" available to the outside

init: # this is the beginning of our binary later.
  mov $0x0e, %ah # sets AH to 0xe (function teletype)
  mov $msg, %bx   # sets BX to the address of the first byte of our message
  mov (%bx), %al   # sets AL to the first byte of our message
  int $0x10 # call the function in ah from interrupt 0x10
  hlt # stops executing

msg: .asciz "Hello world!" # stores the string (plus a byte with value "0") and gives us access via $msg

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
.word 0xaa55 # magic bytes that tell BIOS that this is bootable
```

（[xnumbersx](https://www.reddit.com/user/xnumbersx)发现[错别字](https://www.reddit.com/user/xnumbersx)）

我们有一个新功能：

```asm
mov $msg, %bx
mov (%bx), %al
```

第一行将第一个字节的*地址*加载到寄存器中`bx`（我们使用整个寄存器，因为地址的长度为16位）。

然后第二行将存储*在地址中*的*值**从`bx`*装入到中`al`，因此消息的第一个字符以结束`al`，因为`bx`指向该地址。

但是现在运行时出现错误`ld`：

```shell
as -o boot.o boot.s
ld -o boot.bin --oformat binary -e init -o boot.bin boot.o
boot.o: In function `init':
(.text+0x3): relocation truncated to fit: R_X86_64_16 against `.text'+a
```

堂，那是什么意思？

事实证明，`msg`在ELF文件（`boot.o`）中移动的地址不适合我们的16位地址空间。我们可以通过告诉`ld`我们程序存储器应该从哪里开始来解决此问题。BIOS将把我们的代码加载到address `0x7c00`，因此我们将通过指定`-Ttext 0x7c00`调用链接器的时间来确定我们的起始地址：

```shell
as -o boot.o boot.s
ld -o boot.bin --oformat binary -e init -Ttext 0x7c00 -o boot.bin boot.o
```

QEmu现在将打印“ H”，这是我们消息文本的第一个字符。

现在，我们可以通过执行以下操作来打印整个字符串：

1. 将字符串的第一个字节（即`msg`）的地址放入除`ax`（因为我们将其用于实际打印）之外的任何寄存器中，例如，使用`cx`。
2. 将地址中的字节加载`cx`到`al`
3. 将其中的值`al`与0（字符串的结尾，感谢`.asciz`）进行比较
4. 如果AL包含0，请转到程序结尾
5. 呼叫中断0x10
6. 地址递增`cx`一
7. 从步骤2重复

x86具有一个特殊的寄存器和一堆特殊的指令来处理字符串的事实也是有用的。
为了使用这些指令，我们将字符串（`msg`）的地址加载到特殊寄存器中`si`，这使我们能够使用便捷的`lodsb`指令，该指令从`si`指向的地址加载一个字节，同时指向`al`该地址并递增该地址`si`。

让我们放在一起：

```asm
.code16 # use 16 bits
.global init

init:
  mov $msg, %si # loads the address of msg into si
  mov $0xe, %ah # loads 0xe (function number for int 0x10) into ah
print_char:
  lodsb # loads the byte from the address in si into al and increments si
  cmp $0, %al # compares content in AL with zero
  je done # if al == 0, go to "done"
  int $0x10 # prints the character in al to screen
  jmp print_char # repeat with next byte
done:
  hlt # stop execution

msg: .asciz "Hello world!"

.fill 510-(.-init), 1, 0 # add zeroes to make it 510 bytes long
.word 0xaa55 # magic bytes that tell BIOS that this is bootable
```

让我们看看QEmu中的这段新代码：

![img](http://50linesofco.de/images/post-images/qemu-final-bootloader.png)

🎉耶！🎉

它通过从`print_char`到循环循环打印消息，`jmp print_char`直到我们在中打零字节（该消息的最后一个字符之后）`si`。一旦找到零字节，就跳转到`done`并停止执行。

## 英特尔语法版本和 `nasm`

如所承诺的，我还将向您展示替代`nasm`GNU汇编器的替代使用方式。

首先，首先是：`nasm`可以自己生成原始二进制文件，并且使用Intel语法：

`operation target, source` -我记得使用“ W，T，F”的顺序-“什么，到，从” ;-)

因此，这是先前代码的nasm兼容版本：

```asm
[bits 16]    ; use 16 bits
[org 0x7c00] ; sets the start address

init: 
  mov si, msg  ; loads the address of "msg" into SI register
  mov ah, 0x0e ; sets AH to 0xe (function teletype)
print_char:
  lodsb     ; loads the current byte from SI into AL and increments the address in SI
  cmp al, 0 ; compares AL to zero
  je done   ; if AL == 0, jump to "done"
  int 0x10  ; print to screen using function 0xe of interrupt 0x10
  jmp print_char ; repeat with next byte
done:
  hlt ; stop execution

msg: db "Hello world!", 0 ; we need to explicitely put the zero byte here

times 510-($-$$) db 0           ; fill the output file with zeroes until 510 bytes are full
dw 0xaa55                       ; magic number that tells the BIOS this is bootable
```

（感谢Reddit用户[pahefu](https://www.reddit.com/user/pahefu)在此处指出一个错字！

将其保存后`boot.asm`，可以通过运行进行编译`nasm -o boot2.bin boot.asm`。

请注意，的参数`cmp`顺序`as`与`[org]`nasm和`.org`in `as`中使用的顺序相反，并且**不是**同一回事！

NASM不做通过ELF文件（额外的步骤`boot.o`），所以它不会动我们的`msg`周围，如内存`as`和`ld`一样。

但是，如果我们忘记将代码的起始地址设置为`0x7c00`，则二进制文件用于的地址`msg`仍将是错误的，因为nasm默认情况下采用不同的起始地址。当我们将其显式设置为`0x7c00`（BIOS加载代码的位置）时，地址将在二进制文件中正确计算，并且代码的工作方式与其他版本相同。